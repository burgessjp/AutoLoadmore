apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


def prjVersion = "0.0.1"
def prjGroup = "me.solidev.library"
def prjName = "AutoLoadMore"
def prjArtifactId = "auto-loadmore"
def prjDescription = "library for recyclerview auto load more"
def siteUrl = 'https://github.com/burgessjp/AutoLoadmore'      // 项目的主页
def gitUrl = 'https://github.com/burgessjp/AutoLoadmore.git'   // Git仓库的url
def pckName = "AutoLoadmore"
def bintrayRepo = "maven"

version = prjVersion
group = prjGroup
project.archivesBaseName=prjArtifactId
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            // Add your description here
            name prjName
            version prjVersion
            groupId prjGroup
            description prjDescription
            url siteUrl
            artifactId prjArtifactId
            // Set your license
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'solid'
                    name 'solid'
                    email '1104160707@qq.com'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }

    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publish = true
    dryRun = false
    pkg {
        repo = bintrayRepo
        name = pckName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        version {
            name = prjVersion
            desc = prjDescription
            vcsTag = prjVersion

            gpg {
                sign = true
            }
        }
    }
}